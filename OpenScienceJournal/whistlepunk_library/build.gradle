apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

apply from: 'dependencyVersions.gradle'

buildscript {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 10
        versionName "1.3.4"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        warning "MissingTranslation"
    }

    sourceSets {
        main {
            java {
                srcDir '../../third_party/jsyn'
                srcDir 'src/api/wireapi/java'
            }
            proto {
                srcDir 'src/main/proto'
            }
            aidl {
                srcDir 'src/api/wireapi/aidl'
            }
        }
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
            java.srcDir 'src/robolectricTest/java'
            java.srcDir 'src/test/java'
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':scalarinputapi')

    implementation deps.glide
    annotationProcessor deps.glideCompiler

    implementation deps.dagger
    annotationProcessor deps.daggerCompiler

    implementation deps.cardview
    implementation deps.exifinterface
    implementation deps.recyclerview
    implementation deps.constraintlayout
    implementation deps.material
    implementation deps.expandablerecyclerview
    implementation deps.swiperefreshlayout

    implementation deps.rxJava
    implementation deps.rxAndroid
    implementation deps.rxbinding
    implementation deps.rxPermissions

    implementation deps.jodaTime
    implementation deps.jodaConvert

    implementation deps.crypto
    implementation deps.jwtdecode
    implementation deps.androidsvg
    implementation deps.protobufLite
    // Depend on jsyn maven repo.
    implementation deps.jsyn

    implementation deps.arcore
    implementation deps.sceneform

    implementation(deps.guava) {
        // Avoid error "Program type already present: javax.annotation.CheckForNull"
        exclude group: 'com.google.code.findbugs'
        // Avoid error "Program type already present: org.checkerframework.checker.nullness.compatqual.NullableType"
        exclude group: 'org.checkerframework'
        // Avoid error "Program type already present: com.google.j2objc.annotations.AutoreleasePool"
        exclude group: 'com.google.j2objc'
        // Avoid error "Program type already present: org.codehaus.mojo.animal_sniffer.IgnoreJRERequirement"
        exclude group: 'org.codehaus.mojo'
    }

    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    implementation(deps.googleApiClient) {
        // Avoid error "Program type already present: com.google.common.annotations.Beta".
        exclude group: 'com.google.guava'
        // Avoid error "Program type already present: javax.annotation.CheckForNull"
        exclude group: 'com.google.code.findbugs'
        // Avoid error "Program type already present: com.google.j2objc.annotations.AutoreleasePool"
        exclude group: 'com.google.j2objc'
    }

    implementation(deps.driveApi) {
        // Avoid error "Program type already present: com.google.common.annotations.Beta".
        exclude group: 'com.google.guava'
        // Avoid error "Program type already present: javax.annotation.CheckForNull"
        exclude group: 'com.google.code.findbugs'
        // Avoid error "Program type already present: com.google.j2objc.annotations.AutoreleasePool"
        exclude group: 'com.google.j2objc'
    }

    debugImplementation deps.leakCanary
    releaseImplementation deps.leakCanaryNoop

    testImplementation deps.junit
    androidTestImplementation(deps.junit) {
        exclude module: 'hamcrest-core'
    }
    testImplementation(deps.robolectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}
